import 'http://www.designer.codeskills/data/Data'
import 'http://www.eclipse.org/uml2/5.0.0/UML'
import SCHEMA : 'http:///org/eclipse/datatools/modelbase/sql/schema.ecore'
import SQL : 'http:///org/eclipse/datatools/modelbase/sql/query/SQLQueryModel.ecore'

package data

context Project

inv simpleselect:
	files->select(f | f.ext.toLower() = 'sql' and f.model->notEmpty()).model->selectByKind(SQL::QuerySelectStatement)
			.queryExpr.query->selectByKind(SQL::QuerySelect)->exists(q | 
				q.columnList->isEmpty() and 
				q.fromClause->exists(tr | tr.name.toLower() = 'departments') and 
				q.whereClause->isEmpty() and 
				q.groupByClause->isEmpty())
				
inv simplefilter:
	files->select(f | f.ext.toLower() = 'sql' and f.model->notEmpty()).model->selectByKind(SQL::QuerySelectStatement)
			.queryExpr.query->selectByKind(SQL::QuerySelect)->exists(q | 
				q.fromClause->exists(tr | tr.name.toLower() = 'employees') 
				and q.whereClause->selectByKind(SQL::PredicateBasic)->exists(pb | pb.rightValueExpr->selectByKind(SQL::ValueExpressionSimple)->exists(rv | rv.value = '2000') and
																				  pb.leftValueExpr->selectByKind(SQL::ValueExpressionFunction)->exists(f | f.name.toLower() = 'year' and 
																				  																		   f.parameterList->size() = 1 and 
																				  																		   f.parameterList->exists(pl | pl.name.toLower() = 'hire_date'))
				)
				and q.groupByClause->isEmpty())	
				
inv createumlfile:
	files->select(f | f.name.toLower() = 'myfirstmodel.uml' and f.model->notEmpty())->notEmpty()
	

inv simplediagram:
	files->select(f | f.name.toLower() = 'myfirstmodel.uml' and f.model->notEmpty()).model->selectByKind(UML::Model).allOwnedElements()->selectByKind(UML::Class)
    		->exists(c | c.name.toLower() = 'employee' and 
    			         c.ownedAttribute->size() = 3 and
    			         c.ownedAttribute->exists(p | p.name.toLower() = 'firstname') and
    			         c.ownedAttribute->exists(p | p.name.toLower() = 'lastname') and
    			         c.ownedAttribute->exists(p | p.name.toLower() = 'birthdate')
    		) and
    files->select(f | f.name.toLower() = 'myfirstmodel.uml' and f.model->notEmpty()).model->selectByKind(UML::Model).allOwnedElements()->selectByKind(UML::Class)
    		->exists(c | c.name.toLower() = 'department' and 
    			         c.ownedAttribute->size() = 1 and
    			         c.ownedAttribute->exists(p | p.name.toLower() = 'name')
    		)
				
inv join:
	files->select(f | f.ext.toLower() = 'sql' and f.model->notEmpty()).model->selectByKind(SQL::QuerySelectStatement)
			.queryExpr.query->selectByKind(SQL::QuerySelect)->exists(q | 
				q.fromClause->selectByKind(SQL::TableJoined)->exists(tj | (tj.tableRefLeft.name.toLower() = 'employees' and tj.tableRefRight.name.toLower() = 'salaries') 
																		  or
																		  (tj.tableRefLeft.name.toLower() = 'salaries' and tj.tableRefRight.name.toLower() = 'employees')															
																	) and
				q.whereClause->isEmpty() and 
				q.groupByClause->isEmpty()
			)
			
inv groupby:	
	files->select(f | f.ext.toLower() = 'sql' and f.model->notEmpty()).model->selectByKind(SQL::QuerySelectStatement)
			.queryExpr.query->selectByKind(SQL::QuerySelect)->exists(q | 
				q.selectClause->size() = 2 and
				q.selectClause->selectByKind(SQL::ResultColumn).valueExpr->selectByKind(SQL::ValueExpressionColumn)->exists(expr | expr.name.toLower() = 'gender') and
				q.selectClause->selectByKind(SQL::ResultColumn).valueExpr->selectByKind(SQL::ValueExpressionFunction)->exists(expr | expr.name.toLower() = 'sum' and expr.parameterList->exists(pl | pl.name.toLower() = 'salary')) 
				and
				q.fromClause->selectByKind(SQL::TableJoined)->exists(tj | (tj.tableRefLeft.name.toLower() = 'employees' and tj.tableRefRight.name.toLower() = 'salaries') 
																		  or
																		  (tj.tableRefLeft.name.toLower() = 'salaries' and tj.tableRefRight.name.toLower() = 'employees')	
																	) and
				q.whereClause->isEmpty() and 
				q.groupByClause->size() = 1 and
				q.groupByClause->selectByKind(SQL::GroupingExpression).valueExpr->exists(expr | expr.name.toLower() = 'gender')
			)
			
inv association:
    files->select(f | f.name.toLower() = 'myfirstmodel.uml' and f.model->notEmpty()).model->selectByKind(UML::Model).allOwnedElements()->selectByKind(UML::Association)
    	->exists(a | a.memberEnd->exists(m1, m2 | m1.upperValue.oclIsTypeOf(UML::LiteralUnlimitedNatural) and 
    										(m1.lowerValue.oclIsTypeOf(UML::LiteralInteger) implies m1.lowerValue.integerValue() = 1) and
    										 m1.type.name.toLower() = 'employee' and m2.type.name.toLower() = 'department')
    			)
    			
inv generalization:
    files->select(f | f.name.toLower() = 'myfirstmodel.uml' and f.model->notEmpty()).model->selectByKind(UML::Model).allOwnedElements()->selectByKind(UML::Class)
    	->exists(c1, c2 |  c1.name.toLower() = 'male' and c2.name.toLower() = 'female' and 
    					   c1.superClass->exists(sc | sc.name.toLower() = 'employee') and 
    					   c2.superClass->exists(sc | sc.name.toLower() = 'employee')  
    	)
			
endpackage